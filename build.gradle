buildscript {
    ext.kotlin_version = '1.2.21'
    ext.dokka_version = '0.9.14'

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}

group 'io.intrepid'
version '1.0-SNAPSHOT'

apply plugin: 'kotlin'

apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.8.0"
}

task kotlinJacocoTestReport(type: JacocoReport, dependsOn: 'test') {
    reports {
        xml.enabled = true
        xml.destination file("${project.projectDir}/jacocoRaw.xml")
    }

    sourceDirectories = files(["${project.projectDir}/src/main/kotlin"])
    classDirectories = fileTree(dir: "${buildDir}/classes/kotlin/main")
    executionData = files("${buildDir}/jacoco/test.exec")
}

task jacocoFix(type: Copy) {
    from 'jacocoRaw.xml'
    into './'
    rename { String fileName ->
        fileName.replace("jacocoRaw.xml", "jacoco.xml")
    }
    filter { line -> line.replaceAll("io/", "src/main/kotlin/io/") }
}

apply plugin: 'org.jetbrains.dokka'

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir
    inputs.dir 'src/main/kotlin'
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testCompile "junit:junit:4.12"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}